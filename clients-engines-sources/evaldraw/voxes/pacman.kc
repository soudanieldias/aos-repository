(x,y,z,t,&r,&g,&b)
if (abs(z) > .34) return(0); //early-out
if (abs(x^2+y^2-.554) > .45) return(0); //early-out
//r = 128; g = 128; b = 128; return(1);
if (glob[0] != t) //pre-calculate cos&sin
{     //eat counter :)
   moveto(xres*.5,10); printnum(int(t*2));
   glob[0] = t; t *= .7;
   glob[2] = cos(t); glob[3] = sin(t);
   t += .3; glob[1] = .5-.5*sin(t*16+cos(t*16)*.5);
}

   //Put dots in motion are circle
ox = x; oy = y;
x = glob[2]*ox + glob[3]*oy;
y = glob[2]*oy - glob[3]*ox;

ax = abs(x); ay = abs(y); //Draw 8 dots w/symmetry
if ((max(ax,ay)-cos(PI/8)/sqrt(2))^2
   +(min(ax,ay)-sin(PI/8)/sqrt(2))^2 + z^2 < .06^2)
   { r = 192; g = 255; b = 255; return(1); }

   //Put Pacman in motion around circle
x = (glob[2]*ox - glob[3]*oy)*3+2;
y = (glob[2]*oy + glob[3]*ox)*3;
z *= 3;

   //Render Pacman's eyes
ix = .40; iy = .50; iz = .70;
if ((abs(x)-ix)^2 + (y-iy)^2 + (z+iz)^2 < .24^2)
{
   if ((abs(x+glob[2]*.20)-ix)^2 +
       (y-iy-.10-abs(glob[3])*.20)^2 +
       (z+iz+.10)^2 < .15^2) r = 0; else r = 255; 
   g = r; b = r; return(1);
}

d = x^2+y^2+z^2;
if (d > 1^2) return(0); //Pacman's sphere
if (abs(z-.1) < y*glob[1]) return(0); //mouth

if (d < .9^2) { r =  96; g =  96; b = 48; } //Mouth
         else { r = 192; g = 192; b = 96; } //Body
return(1);